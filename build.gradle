group = 'com.cn'
version = '0.1'

buildscript { //构建脚本
    //定义版本号
    ext {
        springBootVersion="2.1.5.RELEASE"
    }
    repositories {//jar包仓库
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    idea {
        module {
            inheritOutputDirs = true
        }
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '11'

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    ext {
        springBootAdminVersion = '2.1.5'
        springCloudVersion = 'Greenwich.SR1'
    }
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'

//        jdk11 启用jaxb
        implementation 'javax.xml.bind:jaxb-api:2.3.0'
        implementation 'com.sun.xml.bind:jaxb-impl:2.3.0'
        implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.0'
        implementation 'javax.activation:activation:1.1.1'

        runtime 'org.springframework.boot:spring-boot-devtools'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    dependencyManagement {
        imports {
            mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

//springboot2 这里一定得要 在多模块下，不然编译失败，因为不会把依赖模块给打包。
jar {
    enabled = true
}
